1 TL=53261:SW=64:SL=6:SV=38:ST=20:SP=32
2 LF$=CHR$(10):CR$=CHR$(9):LC$=CHR$(13):CS$=CHR$(12)
3 CU$=CHR$(11):CH$=CU$:FORI=1TO15:CH$=CH$+CU$:NEXT
4 VC=236:VB=187:VX=15:VU=189:VD=190:VW=230:VS=45:VV=231:R$=""
5 WB=173:WL=214:WC=14:WW=240:WG=25:YW=161:YS=8:YD=128:YF=4:SP$=""
6 YW$=CHR$(YW):WL$=CHR$(WL):VK$="0123456789EUIOJKL":VG$=VK$
7 DIMD(3),G(73),P(8),N(8),S(4),T(2),M$(18),MS(18),N1(18)
8 DL$="":TS=0:TN=0:CF=0:D$=CH$:FORI=1TOSL+7:D$=D$+LF$:NEXT
9 D5$=LEFT$(D$,SL+18):D6$=LEFT$(D$,SL+19):D9$=LEFT$(D$,SL+22)
10 FORI=1TOSV+2:SP$=SP$+" ":NEXT:FORI=1TO30:R$=R$+CR$:NEXT
11 R1$=LEFT$(R$,21):R8$=LEFT$(R$,8):R7$=LEFT$(R$,17)
12 FORI=1TO32:READJ:NEXT:FORI=0TO18:READM$(I),MS(I),N1(I):NEXT
13 GOSUB56000
14 GOTO1000
17 FORI=1TO9:GC$=" ":POKE11,34:POKE12,2:J=USR(X):NEXT:TV=0
18 FORI=1TO90:GC$=" ":POKE11,34:POKE12,2:J=USR(X):IFASC(GC$)<>SPTHEN21
19 NEXT
20 TV=1
21 PRINTD$;SP$:RETURN
36 FORI=1TODF*14:NEXT:IFDL$="D"THENDL$="":RETURN
37 PRINTD$;SP$:PRINTSP$:IFDL$="W"THENDL$="":RETURN
38 IFCS>77-INT(2*P1^2.5)THENCS=77-INT(2*P1^2.5)
39 IFPS<7THENPS=7
40 PT=INT(42*(P1+1)^LOG(P1^3.7))+75:IFPS>PTTHENPS=PT
41 IFC>125-(INT(P1)*12.5)THENC=125-INT(INT(P1)*12.5)
42 PRINTD5$;J$;" the ";P$
43 PRINT"Treasure   =";TS;LC$;R1$;" Combat str =";CS
44 PRINT"Experience =";EX;LC$;R1$;" Psi power  =";PS
45 PRINT"Turns      =";TN;LC$;R1$;" Stamina    =";C
46 IFCF=1THEN49
47 PRINTSP$
48 RETURN
49 PRINTD9$;">> ";M$;" <<";LC$;R1$;" M Str =";MS;N;" "
50 RETURN
1000 PRINTCS$;LF$;:INPUT "Your name ";J$:IFJ$=""THEN1000
1010 IFLEN(J$)>12THENPRINTLF$;"Too long":GOTO1000
1020 P$="Barbarian":P1=0.5:C1=2:CS=28:PS=22:EX=5:C=150
1030 PRINTLF$;"Good luck, ";J$;" the ";P$
1031 PRINTLF$;"** Press C to continue **"
1032 GC$=" ":POKE11,34:POKE12,2:J=USR(X):IFASC(GC$)=67THEN1040
1033 TI=RND(1):GOTO1032
1040 DF=150:DL$="D":GOSUB36:GOSUB10000:DF=5:GOSUB36
1050 GOTO2000
1500 GC$=" ":POKE11,34:POKE12,2:J=USR(X):IFASC(GC$)<>SPTHEN1510
1501 GOTO1500
1510 FORI=1TOLEN(VG$)
1520 IFMID$(VG$,I,1)=GC$THENRETURN
1530 NEXT
1540 GOTO1500
2000 M=W:PK=PEEK(W):POKEM,VC
2010 C=C+10
2020 IFPK=VUORPK=VDTHEN2040
2030 PRINTD$;"Which direction ?":GOTO2060
2040 PRINTD$;"On the path...which way?"
2060 GOSUB1500:IFGC$="E"THEN45000
2062 IFGC$="U"THENGC$="4":GOTO2070
2063 IFGC$="I"THENGC$="5":GOTO2070
2064 IFGC$="O"THENGC$="6":GOTO2070
2065 IFGC$="J"THENGC$="1":GOTO2070
2066 IFGC$="K"THENGC$="2":GOTO2070
2067 IFGC$="L"THENGC$="3"
2070 A=VAL(GC$):IFA=0THEN2060
2080 IFA>3THENA=A-3:GOTO2080
2090 W=M+A-2-SW*(INT((VAL(GC$)-1)/3)-1)
2100 TN=TN+1:PRINTD$;SP$
2110 Q=VC:Q1=PEEK(W):IFQ1=SPORQ1=WGTHEN2190
2120 IFQ1=VXTHEN48000
2130 IFQ1=VBORQ1=YWORQ1=WWTHENTN=TN-1:GOTO2030
2140 IFQ1=VWORQ1=VVORQ1=VSORQ1=WCTHEN9000
2150 IFQ1=YDORQ1=WBTHEN9090
2160 IFQ1=YSTHEN15000
2170 IFQ1=WLOR(GC$="5"ANDPK=WL)THENQ=VC:C=C-20:IFC<=0THEN55000
2180 IFQ1=YFTHEN2800
2190 POKEM,PK:PK=PEEK(W):M=W:POKEM,Q
2200 IFPK=VUORPK=VDORPK=WGTHENDF=5:GOTO2250
2210 RF=RND(1):IFRF<0.33THEN3000
2230 IFRF>0.75THEN2300
2240 PRINTD$;"Nothing here...search on":DF=80
2250 GOSUB36:GOTO2010
2300 RF=INT(RND(1)*6+1):ONRFGOSUB 2340,2380,2380,2410,2410,2440
2320 DF=80:GOSUB36
2330 GOTO2010
2340 PRINTD$;"A circle of evil...run away !"
2350 CS=CS-INT((FL+1)/2):PS=PS-INT((FL+1)/2):C=C-20
2360 IFC<=0THEN55000
2370 RETURN
2380 PRINTD$;"A hoard of gold":TS=TS+INT(FL*RND(1)*100+100)
2400 RETURN
2410 PRINTD$;"The deep magic is here"
2430 GOTO2450
2440 PRINTD$;"A site of ancient power"
2450 PS=PS+2+INT(FL*P1):CS=CS+1+INT(FL*C1):C=C+25
2460 RETURN
2800 POKEM,SP:M=W:PK=SP:POKEM,VC
2810 RN=RND(1):PRINTD$;SP$
2820 IFS=6ANDRN>0.95ANDT(1)=6ANDT(2)=0ANDRT>25THENT(2)=1:GOTO2870
2830 IFS=5ANDRN>0.85ANDT(0)=0THENT(0)=1:GOTO2880
2840 IFS=4ANDRN>0.7ANDT(0)=1ANDT(1)<6ANDFL>T(1)THEN2890
2850 IFRN>0.43THENPRINTD$;"A worthless bauble":GOTO2940
2860 PRINTD$;"A precious stone !           ":GOTO2930
2870 PRINTD$;"You find the Helm of Evanna !":GOTO2930
2880 PRINTD$;"The amulet of Alarian...empty...":GOTO2930
2890 PRINTD$;"An amulet stone...           ":LF$
2900 DF=60:DL$="D":GOSUB36
2910 IFRN>0.85THENPRINT"...but the wrong one !       ":GOTO2940
2920 PRINT"...the stone fits !         ":T(1)=T(1)+1
2930 TS=TS+100*(T(0)+T(1)+T(2)+FL)
2940 DF=80:GOSUB36
2950 GOTO2010
3000 PRINTD$;"** Beware...thou hast encountered **":MS=0:N=0:CF=1
3020 RF=INT(RND(1)*17):IFRF>9ANDRND(1)>0.85THEN3020
3030 IFQ1=WLORPK=WLTHENRF=INT(RND(1)*2+17)
3040 IFRF=16ANDRND(1)<0.7THEN3020
3050 IFFL<5ANDRF=15THEN3020
3060 X$=LEFT$(M$(RF),1)
3070 FORI=1TOLEN(F$)
3080 IFMID$(F$,I,1)=X$THEN3110
3090 NEXT
3100 GOTO3020
3110 M$=RIGHT$(M$(RF),LEN(M$(RF))-1)
3120 IFMS(RF)=0THEN3150
3130 MS=INT((CS*0.3)+MS(RF)+FL^0.2/(RND(1)+1))
3140 IFN1(RF)=0THEN3160
3150 N=INT(N1(RF)*FL^0.2/(RND(1)+1))
3160 U=INT((RF+1)*(FL^1.5))
3170 IFRF>23THENU=INT((RF-22)*FL^1.5)
3180 PRINTLEFT$(R$,12-(LEN(M$))/2);"An evil ";M$
3190 DF=40:GOSUB36
3500 IFRND(1)<0.6THEN4000
3510 PRINTD$;"Attack or Retreat ?"
3520 GOSUB17
3530 IFGC$="R"THEN3900
3540 IFTV=1THEN3600
3550 IFGC$<>"A"THEN4000
3560 DF=30:DL$="D":GOSUB36
3570 PRINTD$;"*** Strike quickly ***"
3580 GOSUB17
3590 IFTV=0THEN3620
3600 PRINTD$;"*** Too slow ***"
3610 HF=0:GOTO3830
3620 E=39*LOG(EX)/3.14
3630 IFGC$="S"THEN4500
3640 IFMS=0THENPRINTD$;"Your sword avails you nought here":GOTO3830
3650 C=C-1
3660 IFC<=0THENPRINTD$;"You fatally exhaust yourself":GOTO55000
3670 RF=RND(1)*10:IFGC$="H"AND(RF<5ORCS>MS*4)THENZ=2:GOTO3730
3690 IFGC$="B"AND(RF<7ORCS>MS*4)THENZ=1:GOTO3730
3700 IFGC$="L"AND(RF<9ORCS>MS*4)THENZ=0.3:GOTO3730
3710 PRINTD$;"Missed it !!"
3720 HF=0:GOTO3830
3730 IFHF=1THEND=MS+INT(RND(1)*9):HF=0:GOTO3760
3740 D=INT((((CS*50*RND(1))-(10*MS)+E)/100)*Z):IFD<0THEND=0
3750 IFCS>(MS-D)*4THENHF=1
3760 MS=MS-D:PRINTD$;"A hit..."
3780 DF=60:DL$="D":GOSUB36
3790 IFD=0THENPRINTD$;R8$;"but...no damage":HF=0:GOTO3830
3800 PRINTD$;R8$;D;" damage":IFMS<=0THEN3860
3810 IFHF=1THENDF=30:DL$="D":GOSUB36
3820 IFHF=1THENPRINT"The ";M$;" staggers defeated"
3830 DF=110:GOSUB36
3840 IFHF=1THEN3570
3850 GOTO4000
3860 PRINTD$;LF$;"...killing the monster..."
3870 EX=EX+U:HF=0:CF=0
3880 DF=80:GOSUB36
3890 GOTO2010
3900 PRINTD$;"Knavish coward !":CF=0
3910 GOTO3880
4000 PRINTD$;"The creature attacks..."
4010 DF=50:DL$="W":GOSUB36
4020 IFMS=0THEN4300
4030 IFMS<NANDN>6ANDRND(1)<0.5THEN4300
4040 MS=MS-1:IFMS<=0THEN4240
4050 RF=INT(RND(1)*10+1)
4060 ONRFGOTO4070,4070,4070,4100,4110,4110,4120,4120,4130,4120
4070 PRINTD$;"It misses you !":GOTO4280
4100 Z=3:PRINTD$;"Head strike !":GOTO4150
4110 Z=1.5:PRINTD$;"Chest struck !":GOTO4150
4120 Z=1:PRINTD$;"Swordarm hit !":GOTO4150
4130 Z=1.3:PRINTD$;"A body blow !"
4150 DF=60:DL$="D":GOSUB36
4160 G=INT((((MS*75*RND(1))-(10*CS)-E)/100)*Z)
4170 IFG<0THENG=0:PRINTD$;"...saved by your armour !":GOTO4280
4180 C=C-G
4190 IFG>9THENCS=INT(CS-G/6)
4200 IFG=0THENPRINT D$;"Shaken...no damage done":GOTO4280
4210 PRINTD$;"You take...";G;" damage..."
4220 IFCS<=0ORC<=0THEN55000
4230 GOTO4280
4240 PRINTD$;"...using its last energy"
4250 EX=INT(EX+U/2):CF=0
4260 DF=100:GOSUB36
4270 GOTO2010
4280 DF=100:GOSUB36
4290 GOTO3570
4300 PRINTD$;"...hurling a lightning bolt at you !    "
4310 G=INT(((180*N*RND(1))-(PS+E))/100):N=N-5:IFG>9THENN=N-INT(G/5)
4320 DF=80:DL$="W":GOSUB36
4330 IFN<=0THENN=0:GOTO4240
4340 IFRND(1)<0.25THEN4410
4350 IFG<=0THENG=0:GOTO4400
4360 PRINTD$;"It strikes home !"
4370 DF=110:GOSUB36
4380 C=C-G:IFG>9THENPS=INT(PS-G/4)
4390 GOTO4210
4400 PRINTD$;"Your psi shield protects you":GOTO4280
4410 PRINTD$;"...missed you !             ":GOTO4280
4500 PRINTD$;"Which spell seek ye ? ":GOSUB17
4510 IFTV=1THEN3600
4520 IFVAL(GC$)=1THEN4540
4530 PRINTD$;"No such spell...     ":GOTO4640
4540 IF4*PS*RND(1)<=NTHEN4590
4550 GOSUB5000
4560 ONSCGOTO4620,4640,4600
4590 PRINTD$;"No use, the beast's psi shields it":GOTO4640
4600 PRINTD$;"The spell saps all your strength"
4610 GOTO55000
4620 DF=100:GOSUB36
4630 GOTO2010
4640 DF=60:GOSUB36
4650 GOTO4000
4660 DF=60:GOSUB36
4670 GOTO3570
5000 C=C-5:IFC<=0THENSC=3:RETURN
5010 PRINTD$;"Sleep you foul fiend so I may escape"
5030 DF=180:GOSUB36
5040 PRINTD$;"The creature staggers..."
5050 DF=40:DL$="D":GOSUB36
5060 IFRND(1)<0.5THEN5090
5070 PRINT"and collapses stunned"
5080 EX=INT(EX+U/2):CF=0:SC=1:RETURN
5090 PRINT"but recovers with a snarl !"
5100 SC=2:RETURN
9000 IFQ1=WCANDPK=WLTHENPRINTD$;"No entry here":GOTO9110
9010 FORI=2TO7:P(I)=0
9030 N(I)=INT(RND(1)*5+4)
9040 IFN(I)=5THEN9030
9050 NEXT
9060 IFS=1THENMP=M
9070 P(2)=INT(RND(1)*30+1):TF=TN:GOTO9130
9090 IFTN>TF+INT(RND(1)*6+1)THEN9130
9100 PRINTD$;"Way barred"
9110 TN=TN-1:C=C-10:DF=100:DL$="W":GOSUB36
9120 GOTO2010
9130 C=C-10:POKEM,SP:POKEW,Q
9140 IFQ1=WBTHENS=1:FL=1
9150 IFQ1=YDANDS=4THENS=1:FL=1
9160 IFQ1=YDANDS=5ORS=6THENS=S-3:FL=FL-4:M=MW
9170 IFQ1=VSTHENS=2:FL=2
9180 IFQ1=VWTHENS=3:FL=3
9189 I=INT(RND(1)*(SL-5))+1
9190 IFQ1=VWORQ1=VSTHEND2$=LEFT$(D$,I):R2$=LEFT$(R$,P(2))
9200 IFQ1=VVTHENS=4:FL=2
9210 IFQ1=WCTHENS=S+3:FL=FL+4:MW=M
9220 ONSGOSUB10000,12000,12010,14000,14010,14010
9230 DF=5:GOSUB36
9240 GOTO2000
10000 PRINTCS$;D$:F$="VAEGH":FL=1:S=1
10010 FORI=0TOSV:POKETL+I,VB:NEXT
10020 FORI=1TOSL:POKETL+(I*SW),VB:POKE TL+(I*SW)+SV,VB:NEXT
10050 FORI=0TOSV:POKETL+((SL+1)*SW)+I,VB:NEXT
10060 IFG(0)<>0THEN10190
10070 M=TL+INT(RND(1)*(SL-1)+1)*SW+1
10080 L=M:MP=M:W=M:G(0)=M:G(1)=VX
10090 FORI=2TO72STEP2
10100 IFRND(1)>0.5THEN10120
10110 PC=VD:L1=L+65:GOTO10130
10120 PC=VU:L1=L-63
10130 IFL1>=TL+SW*(SL+1)ORL1<=TL+SW+2THEN10100
10140 G(I+1)=PC
10150 IFI>2ANDG(I+1)<>G(I-1)THENL1=L+1
10160 G(I)=L1:L=L1:POKEG(I),G(I+1)
10170 NEXT
10180 G(73)=VX
10190 FORI=0TO72STEP2:POKEG(I),G(I+1):NEXT
10220 IFS(O)<>0THEN10280
10230 FORI=0TO4
10240 N1=INT(RND(1)*(SL-1))+1:N2=INT(RND(1)*34)+1:S(I)=TL+(SW*N1)+N2
10260 IFPEEK(S(I))<>SPORPEEK(S(I)+1)<>SPTHEN10240
10270 NEXT
10280 POKES(0),VW:POKES(0)+1,VW:POKES(1),VW:POKES(1)+1,VW
10290 POKES(2),VS:POKES(2)+1,VS:POKES(3),VS:POKES(3)+1,VS
10300 POKES(4),VV:M=MP:W=M
10320 RETURN
12000 F$="AFL":PC=WG:GOTO12020
12010 F$="FAEHL":PC=WW
12020 PK=SP
12030 PRINTCS$
12050 FORI=1TO135
12060 UY=INT(RND(1)*SL)+1:UX=INT(RND(1)*SV)+1:POKE(TL+(UY*SW)+UX),PC
12070 NEXT
12080 PRINTCH$;D2$;R2$;CR$;CR$;WL$;WL$:PRINTR2$;CR$;
12091 FORI=1TO4:PRINTWL$;:NEXT:PRINTWL$
12100 PRINTR2$;WL$;WL$;"  ";WL$;WL$:PRINTR2$;WL$;WL$;CHR$(WC);" ";
12110 PRINTWL$;WL$;WL$:PRINTR2$;CR$;
12120 FORI=1TO4:PRINTWL$;:NEXT:PRINTCR$;WL$;WL$
12130 PRINTR2$;CR$;CR$;CR$;WL$;WL$:PRINTR2$;CR$;CR$;CR$;CR$;WL$
12141 PRINTD$
12150 FORI=0TOSV:POKETL+I,WB:NEXT
12160 FORI=1TOSL:POKETL+(I*SW),WB:POKE TL+(I*SW)+SV,WB:NEXT
12190 FORI=0TOSV:POKETL+((SL+1)*SW)+I,WB:NEXT:W=TL+(SL*SW)+1:POKEW,SP
12210 IFQ1=YDTHENM=MW:W=M
12220 RETURN
14000 F$="CAGE":P=0:H=N(FL):PK=SP:GOTO14020
14010 F$="CBE":P=0:H=N(FL):PK=SP:P(FL)=P(2)
14020 PRINTCS$;CR$;CR$;:FORI=1TOST:PRINTYW$;:NEXT:PRINTYW$
14030 FORI=1TOSL:PRINTCR$;CR$;YW$;"                   ";YW$:NEXT
14060 PRINTCR$;CR$;:FORI=1TOST:PRINTYW$;:NEXT:PRINTYW$
14070 RESTORE:FORI=1TOP(FL)
14080 READV:IFV=100THENRESTORE
14090 NEXT
14100 L1=TL+2
14110 FORJ=1TO3
14120 READD(J):P=P+1
14130 IFD(J)=100THENRESTORE:D(J)=3:P=P+1
14140 NEXT
14150 FORI=0TOH:PC=YW
14160 L=L1+(SW*I):IFL>TL+(SW*SL)THEN14260
14170 IFI=1THENPC=SP
14180 IFD(1)=0THENPC=YW:GOTO14200
14190 POKE L+D(1),PC:PC=YW
14200 IFI=3THENPC=SP
14210 POKEL+D(1)+D(2),PC:PC=YW
14220 IFI=4THENPC=SP
14230 POKEL+D(1)+D(2)+D(3),PC:PC=YW
14240 NEXT
14250 L1=L1+(SW*H)+SW:GOTO14110
14260 L1=TL+2
14270 FORJ=1TO4
14280 L=L1+(SW*J*(H+1))
14290 FOR K=1TO19
14300 IF L>TL+((SL-1)*SW)THEN14350
14310 POKE L+K,PC
14320 IFK=2ORK=3*HORK=17THENPOKEL+K,SP:POKEL+K-SW,SP:POKEL+K+SW,SP
14330 NEXT
14340 NEXT
14350 IFS=5ORS=6THEN14380
14360 IFFL/2=INT(FL/2)THENPOKETL+SW+ST+1,YS:GOTO14380
14370 POKETL+(SW*SL)+3,YS
14380 IFFL=2ORS=5ORS=6THENPOKETL+(SL+1)*SW+6,YD:POKETL+SL*SW+6,SP
14390 IFP(3)=0THENW=TL+SL*SW+6
14400 IFS=5THEN14470
14410 IFS=6THEN14450
14420 PRINTCH$;R1$;LF$;LF$;LF$;LF$;CR$;CR$;CR$;"The Black Tower"
14430 PRINTR1$;CR$;CR$;CR$;"   of Zaexon":PRINTR1$;LF$;LF$;LF$;
14441 PRINTCR$;CR$;CR$;"   Floor ";FL-1:GOTO14490
14450 PRINTCH$;R1$;LF$;LF$;CR$;CR$;CR$;CR$;CR$;" Vounim's"
14460 PRINTR1$;CR$;CR$;CR$;CR$;CR$;"   Lair":GOTO14500
14470 PRINTCH$;R1$;LF$;LF$;CR$;CR$;CR$;CR$;"The Temple Of"
14480 PRINTR1$;CR$;CR$;CR$;CR$;"  Y'Nagioth"
14490 P(FL+1)=P(FL)+P
14500 IFFL<4ORRND(1)<0.3GOTO14570
14510 FORI=1TOINT(RND(1)*5)+2
14520 N1=INT(RND(1)*(ST-1))
14530 N2=INT(RND(1)*SL)
14540 IFPEEK(TL+3+SW*N2+N1)<>SPTHEN14520
14550 POKETL+3+SW*N2+N1,YF
14560 NEXT
14570 RETURN
15000 POKEW,VC:POKEM,SP
15010 PRINTD$;"Stairs...up or down?":TV=FL
15020 VG$="UD":GOSUB1500:VG$=VK$
15030 IFGC$="U"THENFL=FL+1:GOTO15050
15040 FL=FL-1
15050 IFFL>7ORFL<2THEN15080
15060 DF=110:DL$="D":GOSUB36
15070 GOTO9220
15080 PRINTD$;"These stairs are blocked"
15090 DF=60:DL$="D":GOSUB36
15100 FL=TV:GOTO15010
45000 DF=5:DL$="W":GOSUB36
45010 RT=INT(0.067*(EX+TS/3)^0.5+LOG(EX/((TN+1)^1.5)))
45011 IFRT>28THENRT=28
45020 IFRT<0THENRT=0
45030 PRINTD$;"Your rating be";RT
45040 IFT(2)=1THENPRINT"You have the helm of Evanna"
45050 IFT(0)=1THENPRINT"Amulet stones... ";T(1)
45060 DF=250:DL$="W":GOSUB36
45070 IFGC$="E"THENC=C-10:GC$="":GOTO2010
45080 RETURN
48000 PRINTD$;"Thou art safe in a castle":IFCS<20THENCS=20
48010 POKEM,PK:PK=PEEK(W):M=W:POKEM,Q
48015 IFT(2)=1THENDF=50:GOSUB36:GOSUB45000:GOTO49000
48020 PRINT"Wilt thou leave the valley (Y/N) ?"
48030 VG$="YN":GOSUB1500
48040 DF=5:DL$="W":GOSUB36
48050 GOSUB45000:DF=110:DL$="W":GOSUB36
48070 IFGC$="Y"THEN50205
48080 C=150:PRINTD$;"Thy wounds healed...thy sword sharp"
48090 PRINT"Go as the Gods demand...trust no other"
48100 DF=120:GOSUB36:VG$=VK$:GOTO2010
49000 PRINT:PRINT"You win! "
50205 DF=150:DL$="D":GOSUB36
50210 PRINTD$;"      Type RUN to start again"
50220 CLEAR:END
55000 C=0:CS=0:PS=0:CF=0:DF=110:GOSUB36
55020 IFT(1)=6THEN55070
55030 PRINTD$;"Oh what a frail shell"
55040 PRINT"  is this that we call man"
55050 DF=300:DL$="W":GOSUB36:PRINTCS$:GOTO50210
55070 T(0)=0:T(1)=0:TS=0:CS=30:C=150:PS=30
55080 PRINTD$;"Alarian's amulet protects thy soul"
55090 PRINTLF$;"++ Live again ++"
55100 DF=150:GOSUB36:L=G(0):MP=L:M=W:S=1:GOTO9220
56000 POKE527,1:FORI=546TO597:READJ:POKEI,J:NEXT:RETURN 
60000 DATA4,7,3,6,4,4,6,5,3,6,0,3,8,4,3,5,5,3
60001 DATA8,3,4,5,0,6,3,6,4,6,4,7,4,100
60010 DATAAWolfen,9,0,AHob-Goblin,9,0,AOrc,9,0,EFire-Imp,7,3
60020 DATAGRock-Troll,19,0,EHarpy,10,12,AOgre,23,0
60030 DATABBarrow-Wight,0,25,HCentaur,18,14,EFire-Giant,26,20
60040 DATAVThunder-Lizard,50,0,CMinotaur,35,25,CWraith,0,30
60045 DATAFWyvern,36,12,BDragon,50,20,CRing-Wraith,0,45
60050 DATAABalrog,50,50,LWater-Imp,15,15,LKraken,50,0
60100 DATA169,2,32,190,252,32,198,252,208,7,10,208,245,169,32
60101 DATA208,28,74,32,200,253,152,133,252,10,10,10,56,229
60102 DATA252,133,252,138,74,32,200,253,24,152,101,252,168
60103 DATA185,207,253,160,0,41,127,145,105,96,0
