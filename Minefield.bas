3 REM ***************
4 REM ** MINEFIELD **
5 REM ***************
6 REM *** New monitor (MONUK02) needed
7 REM *** Tested by Tim Holyoake, 06/08/2022
8 REM *** on Grant Searle's FPGA UK101 design
10 DIMA(11,11):M=181:C=187:T=11:Y=29:IN=57088:TR=53400
20 E=0:F1=128:F2=135:F3=147:POKE530,1:GOSUB980
30 INPUT"Instructions needed";H$:IF LEFT$(H$,1)<>"N"THEN700
40 INPUT"Are you an expert";H$:IF LEFT$(H$,1)="Y"THENE=1
50 GS=INT(RND(T)*4+1):GOSUB980
60 H$="MINEFIELD":FORA=1TOLEN(H$)
70 POKE53333+A,ASC(MID$(H$,A,1)):NEXT
80 FORI=2TO32:POKETR+I,F1:POKETR+I+704,F2:NEXT
90 FORI=65TO704STEP64:POKETR+I,F3:POKETR+I+31,F3:NEXT
100 POKETR+122,46:POKETR+C-1,29:POKETR+C,190:POKETR+C+1,F1
110 POKE54043,29:POKE53494,15:POKE53775,85:POKE53777,68
120 POKE53779,76:POKE53781,82:POKE53783,63
130 FORH=1TO20:A=RND(Y)
140 J=INT(RND(T)*10+1):K=INT(RND(M)*10+1)
150 IFA(J,K)<>0THEN140
160 IFJ=10ANDK=1THEN140
170 IFJ=10ANDK=2THEN140
180 IFJ=9ANDK=1THEN140
190 IFJ=1ANDK=10THEN140
200 IFJ=1ANDK=9THEN140
210 IFJ=2ANDK=10THEN140
220 A(J,K)=M:IFA>.5THENA(J,K)=C:GOSUB960
230 IFA(J,K)=CTHENPOKEF-1,C-1:POKEF,C:POKEF+1,C-2
240 NEXT:J=1:K=10
250 M1=0:G1=0:G=53443+64*K:IFN>20THENPOKETR+190,M:GOTO300
260 FORJ1=J-1TOJ+1:IFJ1<1THENJ1=1
270 FORK1=K-1TOK+1:IFK1<1THENK1=1
280 IFA(J1,K1)=MTHENM1=M1+1
290 NEXTK1,J1:POKETR+190,M1+48
300 POKEIN,239:G1=G1+1:IFPEEK(IN)=223THEN380
310 IFPEEK(IN)=251THEN410
320 POKEIN,223:IFPEEK(IN)=191THEN440
330 POKEIN,247:IFPEEK(IN)=191THEN470
340 IFE<1ORINT(G1/GS)<>G1/GSTHEN300
350 G=G-1:G2=PEEK(G):POKEG,22:FORA=1TO60:NEXT:POKEG,G2
360 IFPEEK(G-1)=YTHENL=2:GOTO530
370 GOTO300
380 J=J+1:IFJ=<10ANDA(J,K)<>CTHEN400
390 J=J-1:GOTO300
400 POKETR+3*(J-1)+64*K,T:GOTO500
410 K=K-1:IFK>=1ANDA(J,K)<>CTHEN430
420 K=K+1:GOTO300
430 POKETR+3*J+64*(K+1),T:GOTO500
440 J=J-1:IFJ>=1ANDA(J,K)<>CTHEN460
450 J=J+1:GOTO300
460 POKETR+3*(J+1)+64*K,T:GOTO500
470 K=K+1:IFK<=10ANDA(J,K)<>CTHEN490
480 K=K-1:GOTO300
490 POKETR+3*J+64*(K-1),T
500 IFA(J,K)=MTHENL=INT(RND(T)*4+3):GOTO530
510 IFJ=10ANDK=1THENL=1:GOTO530
520 POKETR+3*J+64*K,Y:N=N+1:GOTO250
530 ONLGOTO540,570,580,590,600,610
540 FORA=1TO10:POKE53494,161:GOSUB970
550 POKE53494,15:GOSUB970
560 NEXT:H$="SAFE IN YOUR BASE !!...well done!!":GOTO650
570 H$="Missile strike!!..you're wiped out":GOTO620
580 H$="BOOM!!!YOU'RE DEAD!!":GOTO620
590 H$="WHAT HEAVY FEET YOU HAD!!!":GOTO620
600 H$="GOODBYE DAISY PUSHER!!":GOTO620
610 H$="IT'S NOT YOUR LUCKY DAY!!"
620 GOSUB960
630 FORA=1TO8:POKEF,15+A:GOSUB970
640 NEXT:POKEF,219
650 FORA=1TOLEN(H$):POKE54160+A,ASC(MID$(H$,A,1)):NEXT
660 FORJ=1TO10:FORK=1TO10:IFA(J,K)<>MTHEN690
670 GOSUB960
680 IFPEEK(F)<>219THENPOKEF,M
690 NEXTK,J:POKE11,0:POKE12,253:X=USR(X):RUN
700 PRINT:PRINT"YOUR MISSION - to cross a dangerous minefield!!"
710 PRINT:PRINT"You are symbol ";CHR$(Y);" and your objective ";
720 PRINTCHR$(15);". Avoid"
730 PRINT"the flooded craters ";CHR$(C-1);CHR$(C);CHR$(C-2);
740 PRINT" where previous"
750 PRINT"contestants perished. Your mine detector ";
760 PRINTCHR$(190);CHR$(F1)
770 PRINT"will tell you how many mines are immediately"
780 PRINT"around you,but cannot indicate direction."
790 PRINT"Also its batteries do not last forever!!"
800 PRINT"You may move UP, DOWN, LEFT or RIGHT by keying"
810 PRINT"U, D, L or R"
820 PRINT:PRINT"             ....Good Luck!!":PRINT
830 PRINT:INPUT"  Ready";H$:IFLEFT$(H$,1)<>"Y"THEN700
840 GOSUB980
850 GOTO40
960 F=TR+3*J+64*K:RETURN
970 FORX=1TO60:NEXT:RETURN
980 PRINTCHR$(12);:RETURN
