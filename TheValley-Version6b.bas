1 REM ** The Valley, Tim Holyoake, October 2022, 8K Compukit UK101
2 DATAOrc,Imp,Demon,Roc,Ogre,Centaur,Wumpus,Cusith,Dragon,Wraith,Balrog
3 DIMM$(10):FORI=0TO10:READM$(I):NEXT:K$="0123456789E":V$=K$
4 E$=CHR$(10):B$=CHR$(9):N$=CHR$(13):J=53261:W$=CHR$(161)
5 FORI=1TO16:D$=D$+CHR$(11):NEXT:FORI=1TO13:D$=D$+E$:NEXT
6 FORI=1TO31:Q$=Q$+" ":NEXT:FORI=1TO30:R$=R$+B$:NEXT:GOTO100
7 I$=" ":POKE11,34:POKE12,2:XX=USR(X):RETURN
9 FORI=1TOB*14:NEXT:IFL$="D"THENL$="":RETURN
10 PRINTD$;Q$:PRINTQ$:IFL$="W"THENL$="":RETURN
13 IFX>99THENX=99
15 PRINTLEFT$(D$,24);J$;"..Barbarian":IFY>77THENY=77
16 PRINT"$$$ =";TS;N$;LEFT$(R$,21);"Str =";Y:IFX<7THENX=7
17 PRINT"Exp =";EX;N$;LEFT$(R$,21);"Psi =";X:IFC>125THENC=125
18 PRINT"Mov =";TN;N$;LEFT$(R$,21);"Vim =";C;" ":IFF=1THEN20
19 PRINTQ$:RETURN
20 PRINTLEFT$(D$,28);">> ";M$;O;N;" ":RETURN
21 FORI=1TO9:GOSUB7:NEXT:TV=0:FORI=1TO70:GOSUB7:IFASC(I$)<>32THEN24
22 NEXT
23 TV=1
24 PRINTD$;Q$:RETURN
26 RT=INT(.067*(EX+TS/3)^.5+LOG(EX/((TN+1)^1.5))):IFRT>28THENRT=28
27 IFRT<0THENRT=0
28 PRINTD$;"Rating";RT:IFT0=1THENPRINT"Amulet with";T1;"gems"
29 IFT2=1THENPRINT"Helm of Evanna"
30 B=250:L$="W":GOSUB9:C=C-10:GOTO51
40 PRINTCHR$(12);D$:L=1:S=1:FORI=0TO38:POKEJ+I,187:NEXT
41 FORI=1TO6:POKEJ+(I*64),187:POKEJ+(I*64)+38,187:NEXT
42 FORI=0TO38:POKE53709+I,187:NEXT:IFSS<>0THEN44
43 M=53392:MP=M:W=M
44 POKE53392,15:SS=53616:POKESS,231:M=MP:W=M:B=5:GOSUB9:RETURN
45 GOSUB7:IFASC(I$)<>32THEN47
46 GOTO45
47 FORI=1TOLEN(V$):IFMID$(V$,I,1)=I$THENRETURN
48 NEXT:GOTO45
50 M=W:PK=PEEK(W):POKEM,236
51 C=C+10:PRINTD$;"Move?"
52 GOSUB45:IFI$="E"THENB=5:L$="W":GOSUB9:GOTO26
55 A=VAL(I$):I=A:IFA=0THEN52
56 IFA>3THENA=A-3:GOTO56
57 W=M+A-2-64*(INT((I-1)/3)-1):TN=TN+1:PRINTD$;Q$
58 Q=236:T=PEEK(W):IFT=32THEN65
59 IFT=15THEN800
60 IFT=187ORT=161THENTN=TN-1:GOTO52
61 IFT=231THEN900
62 IFT=128THENP3=0:GOTO909
63 IFT=8THENPOKEW,236:POKEM,32:GOTO71
64 IFT=4THEN84
65 POKEM,PK:PK=PEEK(W):M=W:POKEM,Q
66 R=RND(1):IFR<.3THEN300
67 IFR>.7THENGOTO77
69 GOSUB9:GOTO51
71 B=80:PRINTD$;"Up/Down?":TV=L:V$="UD":GOSUB45:V$=K$
72 IFI$="U"THENL=L+1:GOTO74
73 L=L-1
74 IFL>7ORL<2THEN76
75 L$="D":GOSUB9:GOTO922
76 PRINTD$;"Barred  ":L$="D":GOSUB9:L=TV:GOTO71
77 B=80:ONINT(RND(1)*6+1)GOSUB79,82,82,83,83,83:GOTO69
79 PRINTD$;"Evil aura"
80 Y=Y-INT((L+1)/2):X=X-INT((L+1)/2):C=C-20:IFC<=0THEN550
81 RETURN
82 PRINTD$;"Gold!":TS=TS+INT(L*RND(1)*100+100):RETURN
83 PRINTD$;"Power!":X=X+2+INT(L*.5):Y=Y+1+INT(L*2):C=C+25:RETURN
84 B=80:POKEM,32:M=W:PK=32:POKEM,236:I=RND(1):PRINTD$;Q$
85 IFI>.9ANDT1=6ANDT2=0ANDRT>20THENT2=1:GOTO90
86 IFI>.8ANDT0=0THENT0=1:GOTO91
87 IFI>.7ANDT0=1ANDT1<6ANDL>T1THEN92
88 IFI>.5THENPRINTD$;"A bauble":GOTO96
89 PRINTD$;"A jewel!":GOTO95
90 PRINTD$;"The Helm!":GOTO95
91 PRINTD$;"The Amulet!":GOTO95
92 PRINTD$;"A gem..";:L$="D":GOSUB9
93 IFRND(1)>.9THENPRINT"wrong one!":GOTO96
94 PRINT"it fits!":T1=T1+1
95 TS=TS+99*(T0+T1+T2+L)
96 GOSUB9:GOTO51
100 PRINTCHR$(12);E$;:INPUT"Name";J$:Y=28:X=22:EX=5:C=150
105 B=150:L$="D":GOSUB9:GOSUB40:GOTO50
300 B=70:PRINTD$;"Beware! ";:O=0:N=0:F=1
302 R=INT(RND(1)*11):IFR>6ANDRND(1)>.8THEN302
304 IFR=10ANDRND(1)<.7THEN302
305 IFL<5ANDR=9ORR=7THEN302
311 M$=M$(R):IFPEEK(598+R)=0THEN315
313 O=INT((Y*.3)+PEEK(598+R)+L^.2/(RND(1)+1))
314 IFPEEK(609+R)=0THEN316
315 N=INT(PEEK(609+R)*L^.2/(RND(1)+1))
316 U=INT((R+1)*(L^1.5))
317 IFR>23THENU=INT((R-22)*L^1.5)
318 PRINTM$;"!":GOSUB9
350 IFRND(1)<.6THEN400
351 PRINTD$;"Attack/Retreat?":GOSUB21:IFI$="R"THEN390
354 IFTV=1THEN360
355 IFI$<>"A"THEN400
356 L$="D":GOSUB9
357 PRINTD$;"*Fight*":GOSUB21:IFTV=0THEN362
360 PRINTD$;"*Too slow*":H=0:GOTO383
362 E=12.2*LOG(EX):IFI$="S"THEN455
364 IFO=0THENPRINTD$;"?!?":GOTO383
365 C=C-1:IFC<=0THENPRINTD$;"Vim gone":GOTO550
367 R=RND(1)*10:IFI$="H"AND(R<5ORY>O*4)THENZ=2:GOTO373
369 IFI$="B"AND(R<7ORY>O*4)THENZ=1:GOTO373
371 PRINTD$;"Miss!":H=0:GOTO383
373 IFH=1THEND=O+INT(RND(1)*9):H=0:GOTO376
374 D=INT((((Y*50*RND(1))-(10*O)+E)/100)*Z):IFD<0THEND=0
375 IFY>(O-D)*4THENH=1
376 O=O-D:PRINTD$;"A hit..":L$="D":GOSUB9
379 IFD=0THENPRINTD$;LEFT$(R$,8);"no damage":H=0:GOTO383
380 PRINTD$;LEFT$(R$,8);D;"damage":IFO<=0THEN386
381 IFH=1THENL$="D":GOSUB9:PRINTM$;" swoons"
383 GOSUB9:IFH=1THEN357
385 GOTO400
386 PRINTD$;E$;M$;" dead":EX=EX+U:H=0:F=0
388 GOSUB9:GOTO51
390 PRINTD$;"Knave!":F=0
391 GOTO388
400 B=70:PRINTD$;M$;" fights..":L$="W":GOSUB9:IFO=0THEN430
403 IFO<NANDN>6ANDRND(1)<.5THEN430
404 O=O-1:IFO<=0THEN424
405 ONINT(RND(1)*9+1)GOTO407,407,407,410,411,411,412,412,412
407 PRINTD$;"Missed!":GOTO428
410 Z=3:PRINTD$;"Mauled!":GOTO415
411 Z=2:PRINTD$;"Bashed!":GOTO415
412 Z=1:PRINTD$;"Grazed!"
415 L$="D":GOSUB9
416 G=INT((((O*75*RND(1))-(10*Y)-E)/100)*Z):IFG<0THENG=0
417 C=C-G:IFG>9THENY=INT(Y-G/6)
420 IFG=0THENPRINTD$;"Unhurt!":GOTO428
421 PRINTD$;G;"damage":IFY<=0ORC<=0THEN550
423 GOTO428
424 PRINTD$;"..psi gone"
425 EX=INT(EX+U/2):F=0:GOSUB9:GOTO51
428 GOSUB9:GOTO357
430 PRINTD$;"..psibeam!"
431 G=INT(((180*N*RND(1))-(X+E))/100):N=N-5:IFG>9THENN=N-INT(G/5)
432 L$="W":GOSUB9
433 IFN<=0THENN=0:GOTO424
434 IFRND(1)<.3THEN441
435 IFG<=0THENG=0:GOTO440
436 PRINTD$;"Hit you!":GOSUB9:C=C-G:IFG>9THENX=INT(X-G/4)
439 GOTO421
440 PRINTD$;"Psi shield!":GOTO428
441 PRINTD$;"Missed!":GOTO428
455 GOSUB500:IFSC=3THENPRINTD$;"Vim gone":GOTO550
462 GOSUB9:IFSC=2THEN400
464 GOTO51
500 B=99:C=C-5:IFC<=0THENSC=3:RETURN
501 PRINTD$;"Sleep, foul ";M$:GOSUB9
504 PRINTD$;"It yawns..";:L$="D":GOSUB9:IFRND(1)<.4THEN509
507 PRINT"zzz":EX=INT(EX+U/2):F=0:SC=1:RETURN
509 PRINT"grr!":SC=2:RETURN
550 B=300:C=0:Y=0:X=0:F=0:GOSUB9
552 IFT1=6THEN557
553 PRINTD$;"Oh what a frail shell":PRINT"  is this that we call man"
555 L$="W":GOSUB9:PRINTCHR$(12):END
557 T0=0:T1=0:TS=0:Y=30:C=150:X=30
558 PRINTD$;"Amulet saves":PRINTE$;"+Live again+"
560 GOSUB9:MP=53392:M=W:S=1:GOTO922 
700 P=0:PK=32:PRINTCHR$(12);B$;B$;
705 FORI=1TO20:PRINTW$;:NEXT:PRINTW$
710 PRINTB$;B$;W$;"        ";W$;"          ";W$
711 PRINTB$;B$;W$;"                   ";W$
712 FORI=1TO3:PRINTB$;B$;W$;"        ";W$;"          ";W$:NEXT
713 PRINTB$;B$;W$;"                   ";W$
715 PRINTB$;B$;:FORI=1TO20:PRINTW$;:NEXT:PRINTW$
720 IFL/2=INT(L/2)THENPOKEJ+85,8:GOTO730
725 POKEJ+387,8
730 IFL=2THENPOKEJ+454,128:POKEJ+390,32
735 IFP3=0THENW=J+390:P3=1
740 PRINTLEFT$(D$,16);LEFT$(R$,21);B$;B$;B$;"Tower Lv";L-1
750 IFL<3ORRND(1)<.4THENRETURN
755 FORI=1TOINT(RND(1)*6)+3
760 N1=INT(RND(1)*19):N2=INT(RND(1)*6)
765 IFPEEK(J+3+64*N2+N1)<>32THEN760
770 POKEJ+3+64*N2+N1,4:NEXT:RETURN
800 B=99:PRINTD$;"Safety":IFY<20THENY=20
801 POKEM,PK:PK=PEEK(W):M=W:POKEM,Q
802 IFT2=1THENGOSUB9:GOTO810
803 PRINT"End?"
804 V$="YN":GOSUB45:L$="W":GOSUB9
806 L$="W":GOSUB9:IFI$="Y"THENEND
808 C=150:PRINTD$;"Healed!":B=120:GOSUB9:V$=K$:GOTO51
810 PRINTE$;J$;" wins!":END
900 IFS=1THENMP=M
909 C=C-10:POKEM,32:POKEW,Q:IFT=128ANDS=4THENS=1:L=1
920 IFT=231THENS=4:L=2
922 IFS=1THENGOSUB40
923 IFS=4THENGOSUB700
924 B=5:GOSUB9:GOTO50
