1 REM *** Acey-Ducey game
2 REM *** Program 7 from UK101 Manual
3 REM *** Recovered & enhanced by Tim Holyoake
4 REM *** 25th July 2022
5 REM
10 PRINT "Acey-Ducey"
12 PRINT "You will get 25 hands"
13 H=1:T=100
15 PRINT
17 REM *** Main game loop
19 PRINT "Hand ";H;" You have $";T
20 X=INT(7*RND(67)+6)
21 IF X>12 GOTO 20
30 Y=INT(X*RND(23)+1)
31 IF Y>=X GOTO 30
32 IF Y=1 THEN Y=2
40 A=X
50 GOSUB 500
60 A=Y
70 GOSUB 500
100 PRINT:PRINT "Your bet";:INPUT B
110 IF B<=T GOTO 115
112 PRINT "You don't have that much"
113 GOTO 100
115 PRINT
120 Z=INT(13*RND(99)+2)
121 IF Z>14 THEN GOTO 120
130 A=Z
140 GOSUB 500
150 PRINT
160 IF Z<=Y GOTO 200
170 IF Z>=X GOTO 200
180 PRINT "You win!"
181 PRINT:PRINT
190 T=B+T
195 GOTO 300
200 PRINT "You lose :("
201 PRINT:PRINT
210 T=T-B
220 IF T<=0 GOTO 380
300 H=H+1
310 IF H>25 GOTO 400
320 GOTO 19
380 PRINT "You're out!!"
390 GOTO 530
400 PRINT "That was your 25th and last hand"
410 GOTO 530
500 IF A<11 GOTO 505
501 IF A>14 THEN PRINT "Error":STOP
502 ON A-10 GOTO 522,524,526,528
505 PRINT A;
510 RETURN
522 PRINT "Jack";
523 RETURN
524 PRINT "Queen";
525 RETURN
526 PRINT "King";
527 RETURN
528 PRINT "Ace";
529 RETURN
530 END
